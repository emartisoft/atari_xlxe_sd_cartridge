fsel_backup equ	$4000

command	 equ	$d5e8
sec_offs equ	$d5e9
sec_cnt	 equ	$d5ea
sec_num  equ	$d5eb
cart_ena equ	$d5ef

	org	$8000

:4	nop

; wait for $a000..$bfff loading completion
	lda	#$40
	bit:req	command

	ldx	#reloc_len-1
	mva:rpl	reloc_src,x	reloc_dest,x-

	jmp	reloc_dest

; emulator: stop on black screen
emu
	inc	^40
	jmp	*

reloc_src
	org	r:$2000
reloc_dest

	; restore file selector
	ldx	#0
copy_loop
copy_src
	lda	fsel_backup,x
copy_dest
	sta	$8000,x
	inx
	bne	copy_loop
	inc	copy_src+2
	inc	copy_dest+2
	lda	#$c0
	cmp	copy_dest+2
	bne	copy_loop

	mva	#$40	^4e
	cli

	; jump to file selector
	jmp	$8000

reloc_len equ *-reloc_dest
	ert	reloc_len>128

	run	emu
